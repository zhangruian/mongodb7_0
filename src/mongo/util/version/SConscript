# -*- mode: python -*-

Import([
    'env',
    'version_extra',
    'version_parts',
])

env = env.Clone()

generateReleases = env.Command(
    target=[
        'releases.h',
    ],
    source=[
        '#buildscripts/cheetah_source_generator.py',
        'releases.h.tpl',
        'releases.yml',
    ],
    action=[
        '$PYTHON ${SOURCES[0]} -o ${TARGETS[0]} ${SOURCES[1]} ${SOURCES[2]} ${MONGO_VERSION}',
    ],
)

env.Alias('generated-sources', generateReleases)

generateMongoVersionFile = env.Substfile(
    target=[
        'mongo_version.yml.in',
    ],
    SUBST_DICT=[
        ('@mongo_version@', env['MONGO_VERSION']),
    ]
)

env.AutoInstall(
    target='$PREFIX_SHAREDIR/mongodb',
    source=generateMongoVersionFile,
    AIB_COMPONENT='dist-test',
    AIB_ROLE='base',
)
